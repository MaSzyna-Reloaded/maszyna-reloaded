extends Node3D

## Seconds of the music fade out after a scenery has loaded
const MUSIC_FADE_OUT_TIME: float = 1.0
## Music in the menu, and the little it gains while a scenery loads - 3 dB, not the jump to full
## scale the loading used to make
const MUSIC_MENU_VOLUME_DB: float = -6.0
const MUSIC_LOADING_VOLUME_DB: float = -3.0
## UserSettings section and keys for the starter music.
const MUSIC_SETTINGS_SECTION: String = "sound"
const MUSIC_ENABLED_KEY: String = "music_enabled"
const MUSIC_VOLUME_KEY: String = "music_volume"
const MUSIC_ENABLED_DEFAULT: bool = true
const MUSIC_VOLUME_DEFAULT: float = 75.0
const MUSIC_VOLUME_PERCENT: float = 100.0
## Seconds of the loading screen fade out into the game
const LOADING_FADE_OUT_TIME: float = 1.0
## Seconds into the loading screen fade out before the world starts - the simulation and its sound
## starting together with the fade make its first frames stutter
const SIMULATION_START_DELAY: float = 0.5
## Seconds the loading screen waits for the streaming to fill in around the player before giving up
const STREAMING_WAIT_TIME: float = 30.0
## The chunks around the camera's built before the game is shown - one each way, so at least a
## chunk's length (1 km) in every direction wherever the camera stands in its own
const SURROUNDINGS_CHUNK_RADIUS: int = 1
## The share of the loading screen's progress the scenery's load takes; the surroundings the rest:
## the scenario started (its sounds) up to SURROUNDINGS_STREAMING_PROGRESS, then the cab and the
## streaming around the camera
const SCENERY_LOAD_SHARE: float = 0.9
const SURROUNDINGS_STREAMING_PROGRESS: float = 0.95
## Seconds of each fade of "Exit to menu": game -> spinner -> scenario selector
const EXIT_FADE_TIME: float = 0.5
## Seconds the spinner stays after the scenery has been unloaded
const EXIT_SPINNER_HOLD_TIME: float = 0.5
## Seconds of the fade to black (and the music fade) before quitting
const QUIT_FADE_TIME: float = 0.5
## Command line of the original's eu07.exe, as its Starter.exe gives it: "-s <scenery>.scn" starts
## that scenery of scenery/ without the selector, "-v <vehicle>" puts the player in that vehicle.
## An exported game leaves "-s <scenery>" alone (its templates run no script from the command line,
## main.cpp), but takes "-v" for itself as --verbose and leaves only its value - so a vehicle
## without its "-v" is the one argument that is neither a flag nor the scenery. The editor's binary
## (the editor, tests: "-s addons/gut/gut_cmdln.gd") runs "-s" as a script, so there the engine's
## arguments are never read and the two come after Godot's "--" (OS.get_cmdline_user_args()).
const ARG_SCENERY: String = "-s"
const ARG_VEHICLE: String = "-v"
## Exit code of a scenery started from the command line without a valid game directory
const EXIT_INVALID_GAME_DIR: int = 1
## The 3D world of a scenery - made when one is chosen, freed in the menu, which renders nothing
## behind it
const WORLD_SCENE: PackedScene = preload("world/world.tscn")

## The game's log files of a scenery (GameLogFileHandler), each started afresh with it, beside the
## engine's file log (Log folder, the Debug settings) - none with the file logging off. A run
## without a display (a test, a probe) writes them into HEADLESS_LOG_DIRECTORY there: it shares the
## game's user directory, and starting its own scenery would cut the logs of a game running beside it
const FILE_LOGGING_SETTING: String = "debug/file_logging/enable_file_logging"
const LOG_PATH_SETTING: String = "debug/file_logging/log_path"
const HEADLESS_LOG_DIRECTORY: String = "headless"
## Each log file's loggers and the lowest level it takes: gameplay.log has the scenario's events from
## INFO beside the gameplay (GamePlayLogRecorder)
const GAME_LOG_FILES: Dictionary[String, Array] = {
    "gameplay.log": ["gameplay", "scenario"],
    "scenario.log": ["scenario"],
    "ai.log": ["ai"],
}
const GAME_LOG_LEVELS: Dictionary[String, GameLog.LogLevel] = {
    "gameplay.log": GameLog.LogLevel.INFO,
    "scenario.log": GameLog.LogLevel.DEBUG,
    "ai.log": GameLog.LogLevel.DEBUG,
}

## The trainsets as the scenery declares them - a start without the selector arranges none
const DECLARED_TRAINSET: Array[MaszynaDynamicData] = []

## A scenery started at once, without the selector - as "-s" on the command line does
@export var scenery: String = ""

var _music_tween: Tween
## The level appropriate to the menu or loading screen, before the player's music volume.
var _music_volume_db: float = MUSIC_MENU_VOLUME_DB
## The world of the scenery being played, null in the menu
var _world: SceneryWorld = null
## The handlers of the running scenery's log files, by file name (GAME_LOG_FILES) - registered with
## GameLog under that name
var _log_handlers: Dictionary[String, GameLogFileHandler] = {}


## Before _ready(): the children must not read a cache left by another build; and the original's
## driver thinks for the drivers a scenery declares (SceneryInstancer) before any scenery is loaded
func _enter_tree() -> void:
    GameDataServer.build_check_version()
    DriverServer.implementation_register(SceneryInstancer.DRIVER_IMPLEMENTATION, MaszynaLegacyAIDriver.new())


func _exit_tree() -> void:
    DriverServer.implementation_unregister(SceneryInstancer.DRIVER_IMPLEMENTATION)
    PythonScreenServer.python_runtime_failed.disconnect(_on_python_runtime_failed)


func _ready() -> void:
    UserSettings.config_changed.connect(_apply_music_settings)
    PythonScreenServer.python_runtime_failed.connect(_on_python_runtime_failed)
    _apply_music_settings()
    var exported: bool = OS.has_feature("template")
    var args: PackedStringArray = OS.get_cmdline_user_args()
    if exported:
        args = OS.get_cmdline_args() + args
    var scenery_at: int = args.find(ARG_SCENERY)
    if scenery_at >= 0 and scenery_at + 1 < args.size():
        # started from the command line, the selector screen is skipped - so is its warning
        if not UserSettings.is_maszyna_game_dir_valid():
            print((
                "Invalid game directory: %s - it has no scenery, dynamic and textures folders. "
                + "Unpack MaSzyna Reloaded into the directory of the original MaSzyna, or start "
                + "the game without -s and set the directory there."
            ) % UserSettings.get_maszyna_game_dir())
            get_tree().quit(EXIT_INVALID_GAME_DIR)
            return
        var train_id: String = ""
        var vehicle_at: int = args.find(ARG_VEHICLE)
        if vehicle_at >= 0 and vehicle_at + 1 < args.size():
            train_id = args[vehicle_at + 1]
        elif exported and OS.is_stdout_verbose():
            for at: int in args.size():
                if not at == scenery_at + 1 and not args[at].begins_with("-"):
                    train_id = args[at]
                    break
        start_scenery(args[scenery_at + 1], train_id, DECLARED_TRAINSET)
    elif scenery:
        start_scenery(scenery, "", DECLARED_TRAINSET)
    else:
        $ScenerySelectorScreen.open()
        $BugReport.show_edge_button()


## Loads scenery/<filename> and puts the player in train_id (none: the scenery's own driver) -
## chosen in the selector, or given on the command line; trainset is the player's trainset as the
## selector arranged it (MaszynaIncludeNode.trainset_override), empty for the declared one
func start_scenery(filename: String, train_id: String, trainset: Array[MaszynaDynamicData]) -> void:
    _play_music(MUSIC_LOADING_VOLUME_DB)
    # the world starts while the loading screen fades out, not when it is built, and at the wall
    # clock's speed whatever the last one ran at
    SimulationServer.simulation_pause()
    SimulationServer.simulation_reset_speed()
    HUDServer.hud_set_visible(false)
    $BugReport.hide_edge_button()
    var info: MaszynaSceneryInfo = MaszynaSceneryInfo.read(filename)
    $GameHud.show_scenario(info, train_id)
    # the name the scenery list gave it ("//$l", "//$n" and the file name)
    $LoadingScreen.show_loading(MaszynaSceneryInfo.read_display_name(filename))
    # the selector dissolves into the loading screen and hides once it is done; the loading below
    # blocks the main thread, so it waits for the dissolve not to stutter
    if $ScenerySelectorScreen.visible:
        await $ScenerySelectorScreen.hidden
    # made under the loading screen: its environment and first frames stall the main thread
    _world = WORLD_SCENE.instantiate() as SceneryWorld
    _world.load_progress.connect(_on_world_load_progress)
    _world.scenario_progress.connect(_on_world_scenario_progress)
    _world.load_files_parsed.connect($LoadingScreen.set_files)
    _world.scenery_loaded.connect(_on_scenery_loaded)
    add_child(_world)
    $GameHud.attach_environment(_world.get_environment())
    var log_files: PackedStringArray = []
    if ProjectSettings.get_setting(FILE_LOGGING_SETTING, false):
        var log_directory: String = String(ProjectSettings.get_setting(LOG_PATH_SETTING)).get_base_dir()
        if DisplayServer.get_name() == "headless":
            log_directory = log_directory.path_join(HEADLESS_LOG_DIRECTORY)
        for log_file: String in GAME_LOG_FILES:
            var handler: GameLogFileHandler = GameLogFileHandler.open(log_directory.path_join(log_file))
            if not handler:
                continue
            handler.min_level = GAME_LOG_LEVELS[log_file]
            GameLog.register_handler(log_file, handler)
            for logger_id: String in GAME_LOG_FILES[log_file]:
                GameLog.assign_handler(logger_id, log_file)
            _log_handlers[log_file] = handler
            log_files.append(handler.get_path())
    $GamePlayLogRecorder.start()
    $BugReport.attach_world(_world, log_files)
    await _world.load_scenery(filename, trainset, train_id)
    await _build_surroundings()
    var tween: Tween = create_tween()
    tween.tween_property($LoadingScreen, "modulate:a", 0.0, LOADING_FADE_OUT_TIME)
    tween.parallel().tween_callback(SimulationServer.simulation_unpause).set_delay(SIMULATION_START_DELAY)
    await tween.finished
    $LoadingScreen.visible = false
    $LoadingScreen.modulate.a = 1.0
    HUDServer.hud_set_visible(true)
    $BugReport.show_edge_button()


## The scenery's load on the loading screen, in its share of the progress
func _on_world_load_progress(progress: float, stage: MaszynaIncludeNode.LoadStage, message: String) -> void:
    $LoadingScreen.set_progress(progress * SCENERY_LOAD_SHARE, stage, message)


## The scenario starts as the first part of the surroundings - right after the vehicles
func _on_world_scenario_progress(progress: float) -> void:
    $LoadingScreen.set_progress(
            lerpf(SCENERY_LOAD_SHARE, SURROUNDINGS_STREAMING_PROGRESS, progress),
            MaszynaIncludeNode.LoadStage.SURROUNDINGS, "")


## The player's surroundings, the last stage of the loading screen: the streaming's chunks around
## the camera (SURROUNDINGS_CHUNK_RADIUS) and the vehicles within the draw distance
## (RailVehicleRenderingServer) built. The rest of the draw distance keeps streaming after the game
## appears.
func _build_surroundings() -> void:
    print("[SceneryLoad] SURROUNDINGS started")
    var started_msec: int = Time.get_ticks_msec()
    var deadline: float = started_msec + STREAMING_WAIT_TIME * 1000.0
    var most_pending: int = 0
    var progress: float = SURROUNDINGS_STREAMING_PROGRESS
    while SceneryStreamingServer.streaming_has_camera() and Time.get_ticks_msec() < deadline:
        var pending: int = SceneryStreamingServer.area_get_pending_count(SURROUNDINGS_CHUNK_RADIUS) \
                + RailVehicleRenderingServer.builds_get_pending_count()
        if SceneryStreamingServer.area_is_ready(SURROUNDINGS_CHUNK_RADIUS) \
                and RailVehicleRenderingServer.builds_get_pending_count() == 0:
            break
        most_pending = maxi(most_pending, pending)
        var built: float = 1.0 - float(pending) / most_pending if most_pending > 0 else 0.0
        progress = maxf(progress, lerpf(SURROUNDINGS_STREAMING_PROGRESS, 0.99, built))
        $LoadingScreen.set_progress(
                progress, MaszynaIncludeNode.LoadStage.SURROUNDINGS, "")
        await get_tree().process_frame
    $LoadingScreen.set_progress(1.0, MaszynaIncludeNode.LoadStage.SURROUNDINGS, "")
    print("[SceneryLoad] SURROUNDINGS %.1f s" % ((Time.get_ticks_msec() - started_msec) / 1000.0))


## The Python runtime of the cab screens has ended (PythonScreenServer, once): the screens stay
## blank and the game goes on. The player is told so, with the reason that is in the log as well.
func _on_python_runtime_failed(reason: String) -> void:
    %PythonRuntimeProblem.message = "%s\n\n%s" % [
        tr("The cab screens drawn by Python scripts could not be started and stay blank. "
            + "The game goes on without them."),
        reason,
    ]
    %PythonRuntimeProblem.ask()


## Escape in the scenario selector: fade the screen to black and the music out, then quit
func _on_scenery_selector_quit_requested() -> void:
    if _music_tween:
        _music_tween.kill()
    var tween: Tween = create_tween().set_parallel()
    tween.tween_property($FadeLayer/Black, "modulate:a", 1.0, QUIT_FADE_TIME)
    tween.tween_property($Music, "volume_linear", 0.0, QUIT_FADE_TIME)
    await tween.finished
    get_tree().quit()


## Unloading a scenery stalls the main thread (thousands of nodes), so it happens behind the
## spinner: the game fades into it, and it fades into the scenario selector
func _on_exit_to_menu_pressed() -> void:
    # it takes every key while shown, so the vigilance button's space does not reach the cab
    %ExitConfirmation.ask()


## "Exit to menu", and another game directory chosen in a game: set once the scenery read from the
## old one is gone, before the menu lists the sceneries of the new one
func _exit_to_menu(game_dir: String = "") -> void:
    _play_music(MUSIC_MENU_VOLUME_DB)
    await $SpinnerOverlay.fade_in(EXIT_FADE_TIME)
    # the world stops once the spinner covers it, and stays stopped until the next scenery shows;
    # the menu is heard at the wall clock's speed (TrainSoundSystem)
    SimulationServer.simulation_pause()
    SimulationServer.simulation_reset_speed()
    HUDServer.hud_set_visible(false)
    $BugReport.hide_edge_button()
    # the scenery's script context and environment go with the world
    $GameHud.attach_script_context(RID())
    $GameHud.attach_environment(null)
    $GamePlayLogRecorder.stop()
    for log_file: String in _log_handlers:
        for logger_id: String in GAME_LOG_FILES[log_file]:
            GameLog.unassign_handler(logger_id, log_file)
        GameLog.unregister_handler(log_file)
    _log_handlers.clear()
    $BugReport.attach_world(null, PackedStringArray())
    await _world.unload_scenery()
    _world.queue_free()
    _world = null
    # the world is gone a frame later, and the allocator keeps what it held until asked
    await get_tree().process_frame
    ProcessMemory.release_unused()
    SceneryLoadMeasurement.print_process("SceneryMemory", "menu")
    if game_dir:
        UserSettings.save_maszyna_game_dir(game_dir)
    await get_tree().create_timer(EXIT_SPINNER_HOLD_TIME).timeout
    $ScenerySelectorScreen.open()
    $BugReport.show_edge_button()
    await $SpinnerOverlay.fade_out(EXIT_FADE_TIME)
    if game_dir:
        $ScenerySelectorScreen.show_game_dir_changed()


## Music plays while no scenery is loaded (autoplay) and while a scenery loads, at the level the
## caller asks for
func _play_music(volume_db: float) -> void:
    if _music_tween:
        _music_tween.kill()
    _music_volume_db = volume_db
    _apply_music_settings()
    if not $Music.playing:
        $Music.play()


## The player's sound settings change the starter music immediately, including Discard changes.
func _apply_music_settings() -> void:
    var enabled: bool = bool(UserSettings.get_setting(
            MUSIC_SETTINGS_SECTION, MUSIC_ENABLED_KEY, MUSIC_ENABLED_DEFAULT))
    var volume: float = float(UserSettings.get_setting(
            MUSIC_SETTINGS_SECTION, MUSIC_VOLUME_KEY, MUSIC_VOLUME_DEFAULT))
    $Music.volume_linear = (
        db_to_linear(_music_volume_db) * volume / MUSIC_VOLUME_PERCENT if enabled else 0.0)


## The world's scenario is ready; player initialization is coordinated by the world scene.
func _on_scenery_loaded(_first_train_id: String) -> void:
    $GameHud.attach_script_context(_world.get_script_context())
    _music_tween = create_tween()
    _music_tween.tween_property($Music, "volume_linear", 0.0, MUSIC_FADE_OUT_TIME)
    _music_tween.tween_callback($Music.stop)
